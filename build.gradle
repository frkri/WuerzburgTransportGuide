plugins {
    id 'java'
    id 'application'
    id 'org.javamodularity.moduleplugin' version '1.8.12'
    id 'org.openjfx.javafxplugin' version '0.1.0'
    id 'org.beryx.jlink' version '3.0.1'
    id "org.openapi.generator" version "6.6.0"
}

group 'io.github'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

ext {
    junitVersion = '5.10.1'
}

sourceCompatibility = '20'
targetCompatibility = '20'

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

application {
    mainModule = 'io.github.wuerzburgtransportguide'
    mainClass = 'io.github.wuerzburgtransportguide.HelloApplication'
}

openApiGenerate {
    generatorName.set("java")
    library.set("retrofit2")
    inputSpec.set("$rootDir/specs/netzplan.yml")
    outputDir.set("$buildDir/generated")
    apiPackage.set("io.github.wuerzburgtransportguide.api")
    invokerPackage.set("io.github.wuerzburgtransportguide.invoker")
    modelPackage.set("io.github.wuerzburgtransportguide.model")
    configOptions.set([
            dateLibrary: "java8"
    ])
}

javafx {
    version = '20.0.2'
    modules = ['javafx.controls', 'javafx.fxml']
}

dependencies {
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    testImplementation("org.junit.jupiter:junit-jupiter-api:${junitVersion}")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:${junitVersion}")
}

test {
    useJUnitPlatform()
}

jlink {
    imageZip = project.file("${buildDir}/distributions/app-${javafx.platform.classifier}.zip")
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        name = 'app'
    }
}

jlinkZip {
    group = 'distribution'
}

compileJava.dependsOn tasks.named("openApiGenerate")